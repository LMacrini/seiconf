{
  self,
  lib,
  ...
}:
{
  flake.wrappers.fish =
    {
      inputs',
      wlib,
      pkgs,
      system,
      config,
      ...
    }:
    let
      match =
        builtins.readFile "${inputs'.catppuccin.packages.fish}/Catppuccin Macchiato.theme"
        |> builtins.split "(fish_color_.*?)$";

      theme =
        builtins.elemAt (builtins.elemAt match 1) 0
        |> lib.splitString "\n"
        |> builtins.map (s: "set -g ${s}")
        |> builtins.concatStringsSep "\n";

      conf = pkgs.writeText "config.fish" ''
        ${theme}

        status is-interactive; and begin
          nix-your-shell fish | source
          zoxide init fish | source

          alias cd z
          alias ls lsd
          abbr --add ll ls -l
          abbr --add la ls -A
          abbr --add lt ls --tree
          abbr --add lla ls -lA
          abbr --add llt ls -l --tree
          abbr --add l ls -alh

          abbr --add !tmp --position anywhere "(mktemp -d)"

          set fish_greeting
          bind \cz 'fg 2>/dev/null; commandline -f repaint'

          function fish_command_not_found
            set program $argv[1]

            if not test -f /etc/programs.sqlite
              echo "$program: command not found" >&2
              return
            end

            set packages (
              sqlite3 -cmd ".parameter init" \
                  -cmd ".parameter set @program \"$program\"" \
                  /etc/programs.sqlite \
                  "select package from programs where name = @program and system = \"${system}\";" \
                  2>/dev/null
            )

            if test $status -ne 0
              echo "$program: command not found (programs database error)" >&2
              return
            end

            if test (count $packages) -eq 0
              echo "$program: command not found in PATH or in nixpkgs" >&2
              return
            else if test (count $packages) -eq 1
              echo "The program '$program' is not in your PATH. You can make it available in an" >&2
              echo "ephemeral shell by using the following package from nixpkgs:" >&2
              echo "  $packages[1]" >&2
              return
            end

            echo "The program '$program' is not in your PATH. It is provided by several packages." >&2
            echo "You can make it available in an ephemeral shell by using one of the following" >&2
            echo "packages from nixpkgs:" >&2
            for package in $packages
              echo "  $package" >&2
            end
          end

          if type -q direnv
            direnv hook fish | source
          end

          if type -q jj; and type -q git
            abbr --add git jj git
            abbr --add _git git
          end

          if set -q KITTY_INSTALLATION_DIR
            set --global KITTY_SHELL_INTEGRATION "no-rc no-cursor"
            source "$KITTY_INSTALLATION_DIR/shell-integration/fish/vendor_conf.d/kitty-shell-integration.fish"
            set --prepend fish_complete_path "$KITTY_INSTALLATION_DIR/shell-integration/fish/vendor_completions.d"
          end
        end
      '';
    in
    {
      imports = [
        wlib.modules.default
        self.nixosModules.inputs
      ];
      package = pkgs.fish;
      extraPackages = with pkgs; [
        lsd
        nix-your-shell
        sqlite
        zoxide
      ];

      passthru.shellPath = config.wrapperPaths.relPath;

      flags = {
        "-C" = "source ${conf}";
      };
    };
}
